plugins {
    id "org.jetbrains.kotlin.jvm" version "${kotlin_version}"
    id "org.jetbrains.kotlin.kapt" version "${kotlin_version}"
    id "org.jetbrains.kotlin.plugin.allopen" version "${kotlin_version}"
    id "io.gitlab.arturbosch.detekt" version "${kotlin_detekt_version}"
    id "io.micronaut.application" version "${micronautAppPluginVersion}"

    id 'java'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
    id 'maven-publish'
    id 'signing'
    id 'distribution'
    id "org.owasp.dependencycheck" version "8.1.0"
}

ext {
    kotlin_logging_version = '2.0.10'
    fabric8_version = '6.4.1'
    jackson_module_kotlin_version = '2.14.2'
    infra_repo_version = "1.2.1"
    resource_model_version = '1.3.0'
    slf4j_api_version = '2.0.5'
    slf4j2_impl_version = '2.19.0'
    junit_jupiter_version = '5.9.2'
    commons_text_version = '1.10.0'
    sharedDir = file("${project.rootDir}/shared")
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'java'


group = 'com.exactpro.th2'
version = release_version


repositories {
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    implementation("io.micronaut:micronaut-http-server-netty")

    implementation "io.fabric8:kubernetes-client:${fabric8_version}"

    implementation "org.slf4j:slf4j-api:$slf4j_api_version"
    implementation "org.apache.logging.log4j:log4j-slf4j2-impl:$slf4j2_impl_version"
    implementation "org.apache.logging.log4j:log4j-core:$slf4j2_impl_version"

    implementation "com.exactpro.th2:th2-custom-resource-model:$resource_model_version"
    implementation "com.exactpro.th2:infra-repo:$infra_repo_version"

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlin_logging_version

    implementation "org.apache.commons:commons-text:$commons_text_version"
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: jackson_module_kotlin_version
    implementation "org.junit.jupiter:junit-jupiter:$junit_jupiter_version"

    testImplementation "org.junit.jupiter:junit-jupiter:$junit_jupiter_version"
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: kotlin_version

    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:$kotlin_detekt_version")
}

dependencyCheck {
    formats=['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS=5

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}

application {
    mainClass.set("com.exactpro.th2.converter.ConverterApplicationKt")
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    into("META-INF/maven/$project.group/$project.name") {
        from {
            generatePomFileForMavenJavaPublication
        }
        rename ".*", "pom.xml"
    }

    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

// conditionals for publications
tasks.withType(PublishToMavenRepository) {
    onlyIf {
        (repository == publishing.repositories.nexusRepository &&
                project.hasProperty('nexus_user') &&
                project.hasProperty('nexus_password') &&
                project.hasProperty('nexus_url')) ||
                (repository == publishing.repositories.sonatype &&
                        project.hasProperty('sonatypeUsername') &&
                        project.hasProperty('sonatypePassword')) ||
                (repository == publishing.repositories.localRepo)
    }
}

tasks.withType(Sign) {
    onlyIf {
        project.hasProperty('signingKey') &&
                project.hasProperty('signingPassword')
    }
}

// disable running task 'initializeSonatypeStagingRepository' on a gitlab
tasks.whenTaskAdded { task ->
    if (task.name.equals('initializeSonatypeStagingRepository') &&
            !(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword'))
    ) {
        task.enabled = false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = rootProject.name
                packaging = 'jar'
                description = rootProject.description
                url = vcs_url
                scm {
                    url = vcs_url
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'developer'
                        name = 'developer'
                        email = 'developer@exactpro.com'
                    }
                }
                scm {
                    url = vcs_url
                }
            }
        }
    }
    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
        //Nexus repo to publish from gitlab
        maven {
            name = 'nexusRepository'
            credentials {
                username = project.findProperty('nexus_user')
                password = project.findProperty('nexus_password')
            }
            url = project.findProperty('nexus_url')
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

clean {
    delete sharedDir
}


detekt {
    buildUponDefaultConfig = true
    autoCorrect = true
    config = files("$rootDir/config/detekt/detekt.yml")
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

configurations {
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}